(ns clojure-mcp.core
  (:require [clojure.data.json :as json]
            [clojure.java.io :as io]
            [clojure.spec.alpha :as s]
            [clojure.string :as string]
            [taoensso.timbre :as log]
            [clojure-mcp.nrepl :as nrepl]
            [clojure-mcp.config :as config]
            [clojure-mcp.file-content :as file-content]
            [clojure-mcp.nrepl-launcher :as nrepl-launcher]
            [clojure-mcp.training-log :as training-log])
  (:import [io.modelcontextprotocol.server.transport
            StdioServerTransportProvider]
           [io.modelcontextprotocol.server McpServer
            McpServerFeatures$AsyncToolSpecification
            McpServerFeatures$AsyncResourceSpecification
            McpServerFeatures$AsyncPromptSpecification]
           [io.modelcontextprotocol.spec
            McpSchema$ServerCapabilities
            McpSchema$Tool
            McpSchema$ToolAnnotations
            McpSchema$CallToolRequest
            McpSchema$CallToolResult
            McpSchema$TextContent
            McpSchema$Prompt
            McpSchema$PromptArgument
            McpSchema$GetPromptRequest
            McpSchema$GetPromptResult
            McpSchema$PromptMessage
            McpSchema$Role
            McpSchema$Resource
            McpSchema$TextResourceContents
            McpSchema$ReadResourceResult]
           [reactor.core.publisher Mono]
           [io.modelcontextprotocol.json McpJsonMapper]
           [io.modelcontextprotocol.json.jackson3 JacksonMcpJsonMapper]
           [tools.jackson.databind.json JsonMapper]))

(defonce ^McpJsonMapper json-mapper
  (JacksonMcpJsonMapper. (JsonMapper.)))

(defn create-mono-from-callback
  "Creates a function that takes the exchange and the arguments map and
  returns a Mono promise The callback function should take three
  arguments: 
   - exchange: The MCP exchange object 
   - arguments: The arguments map sent in the request 
   - continuation: A function that will be called with the result and will fullfill the promise"
  [callback-fn]
  (fn [exchange arguments]
    (Mono/create
     (reify java.util.function.Consumer
       (accept [_this sink]
         (callback-fn
          exchange
          arguments
          (fn [result]
            (.success sink result))))))))

(defn- adapt-result [result]
  (cond
    (string? result) (McpSchema$TextContent. result)
    (file-content/file-response? result)
    (file-content/file-response->file-content result)
    :else (McpSchema$TextContent. " ")))

(defn adapt-results ^McpSchema$CallToolResult [list-str error?]
  (-> (McpSchema$CallToolResult/builder)
      (.content (vec (keep adapt-result list-str)))
      (.isError error?)
      (.build)))

(defn- ->tool-annotations
  "Builds an McpSchema$ToolAnnotations from a Clojure annotation map, or nil
   if the map is nil/empty. Keys (all optional):
     :title           - human-readable name (String)
     :read-only?      - Boolean (nil means unspecified)
     :destructive?    - Boolean
     :idempotent?     - Boolean
     :open-world?     - Boolean
     :return-direct?  - Boolean
   nil values are preserved as 'unspecified' per the MCP spec."
  ^McpSchema$ToolAnnotations
  [{:keys [title read-only? destructive? idempotent? open-world? return-direct?]
    :as annotations}]
  (when (seq annotations)
    (McpSchema$ToolAnnotations.
     title read-only? destructive? idempotent? open-world? return-direct?)))

(defn create-async-tool
  "Creates an AsyncToolSpecification with the given parameters.

   Takes a map with the following keys:
    :name         - The name of the tool
    :description  - A description of what the tool does
    :schema       - JSON schema for the tool's input parameters
    :annotations  - (Optional) MCP ToolAnnotations as a Clojure map. See
                    `->tool-annotations` for the recognized keys.
    :service-atom - The atom holding the nREPL client connection.
    :tool-fn      - Function that implements the tool's logic.
                    Signature: (fn [exchange args-map nrepl-client clj-result-k] ... )
                      * exchange     - ignored (or used for advanced features)
                      * arg-map      - map with string keys representing the mcp tool call args
                      * nrepl-client - the validated and dereferenced nREPL client
                      * clj-result-k - continuation fn taking vector of strings and boolean error flag."
  [{:keys [name description schema annotations tool-fn]}]
  (let [schema-json (json/write-str schema)
        tool-annotations (->tool-annotations annotations)
        mcp-tool (cond-> (McpSchema$Tool/builder)
                   true (.name name)
                   true (.description description)
                   true (.inputSchema json-mapper schema-json)
                   tool-annotations (.annotations tool-annotations)
                   true (.build))
        mono-fn (create-mono-from-callback
                 (fn [exchange arg-map mono-fill-k]
                   (let [clj-result-k
                         (fn [res-list error?]
                           ;; Opt-in training-log emit — no-op unless
                           ;; CLOJURE_MCP_TRAINING_DIR env is set.
                           (training-log/record-tool-call! name arg-map res-list error?)
                           (mono-fill-k (adapt-results res-list error?)))]
                     (tool-fn exchange arg-map clj-result-k))))]
    (McpServerFeatures$AsyncToolSpecification.
     mcp-tool
     (reify java.util.function.BiFunction
       (apply [_this exchange request]
         (let [arguments (.arguments ^McpSchema$CallToolRequest request)]
           (log/debug (str "Args from MCP: " (pr-str arguments)))
           (mono-fn exchange arguments)))))))

(defn adapt-prompt-result
  "Adapts a Clojure prompt result map into an McpSchema$GetPromptResult.
   Expects a map like {:description \"...\" :messages [{:role :user :content \"...\"}]}"
  ^McpSchema$GetPromptResult
  [{:keys [description messages]}]
  (let [mcp-messages (mapv (fn [{:keys [role content]}]
                             (McpSchema$PromptMessage.
                              (case role ;; Convert keyword role to McpSchema$Role enum
                                ;; :system McpSchema$Role/SYSTEM
                                :user McpSchema$Role/USER
                                :assistant McpSchema$Role/ASSISTANT
                                ;; Add other roles if needed
                                )
                              (McpSchema$TextContent. content))) ;; Assuming TextContent for now
                           messages)]
    (McpSchema$GetPromptResult. description mcp-messages)))

(defn create-async-prompt
  "Creates an AsyncPromptSpecification with the given parameters.
   
   Takes a map with the following keys:
    :name        - The name (ID) of the prompt
    :description - A description of the prompt
    :arguments   - A vector of maps, each defining an argument:
                   {:name \"arg-name\" :description \"...\" :required? true/false}
    :prompt-fn   - Function that implements the prompt logic.
                   Signature: (fn [exchange request-args clj-result-k] ... )
                     * exchange - The MCP exchange object
                     * request-args - Map of arguments provided in the client request
                     * clj-result-k - Continuation fn taking one map argument:
                                      {:description \"...\" :messages [{:role :user :content \"...\"}]} "
  [{:keys [name description arguments prompt-fn]}]
  (let [mcp-args (mapv (fn [{:keys [name description required?]}]
                         (McpSchema$PromptArgument. name description required?))
                       arguments)
        mcp-prompt (McpSchema$Prompt. name description mcp-args)
        mono-fn (create-mono-from-callback ;; Reuse the existing helper
                 (fn [_ request mono-fill-k]
                   ;; The request object has an .arguments() method
                   (let [request-args (.arguments ^McpSchema$GetPromptRequest request)] ;; <-- Corrected method call
                     (prompt-fn _ request-args
                                (fn [clj-result-map]
                                  (mono-fill-k (adapt-prompt-result clj-result-map)))))))]
    (McpServerFeatures$AsyncPromptSpecification.
     mcp-prompt
     (reify java.util.function.BiFunction
       (apply [_this exchange request]
         (mono-fn exchange request))))))

(defn add-tool
  "Helper function to create an async tool from a map and add it to the server."
  [mcp-server tool-map]
  (.removeTool mcp-server (:name tool-map))
  ;; Pass the service-atom along when creating the tool
  (-> (.addTool mcp-server (create-async-tool tool-map))
      (.subscribe)))

(defn create-async-resource
  "Creates an AsyncResourceSpecification with the given parameters.
   
   Takes a map with the following keys:
    :url          - The URL of the resource
    :name         - The name of the resource
    :description  - A description of what the resource is
    :mime-type    - The MIME type of the resource
    :resource-fn  - Function that implements the resource retrieval logic.
                    Signature: (fn [exchange request clj-result-k] ... )
                      * exchange     - The MCP exchange object
                      * request      - The request object
                      * clj-result-k - continuation fn taking a vector of strings"
  [{:keys [url name description mime-type resource-fn]}]
  (let [resource (-> (McpSchema$Resource/builder)
                     (.uri url)
                     (.name name)
                     (.description description)
                     (.mimeType mime-type)
                     (.build))
        mono-fn (create-mono-from-callback
                 (fn [exchange request mono-fill-k]
                   (resource-fn
                    exchange
                    request
                    (fn [result-strings]
                      ;; Create TextResourceContents objects with the URL and MIME type
                      (let [resource-contents (mapv #(McpSchema$TextResourceContents. url mime-type %)
                                                    result-strings)]
                        ;; Create ReadResourceResult with the list of TextResourceContents
                        (mono-fill-k (McpSchema$ReadResourceResult. resource-contents)))))))]
    (McpServerFeatures$AsyncResourceSpecification.
     resource
     (reify java.util.function.BiFunction
       (apply [_this exchange request]
         (mono-fn exchange request))))))

(defn add-resource
  "Helper function to create an async resource from a map and add it to the server.
   
   Takes an MCP server and a resource map with:
    :url          - The URL of the resource
    :name         - The name of the resource
    :description  - A description of what the resource is
    :mime-type    - The MIME type of the resource
    :resource-fn  - Function that implements the resource retrieval logic."
  [mcp-server resource-map]
  (.removeResource mcp-server (:url resource-map))
  (-> (.addResource mcp-server (create-async-resource resource-map))
      (.subscribe)))

(defn add-prompt
  "Helper function to create an async prompt from a map and add it to the server.
   
   Takes an MCP server and a prompt map with:
    :name        - The name (ID) of the prompt
    :description - A description of the prompt
    :arguments   - A vector of maps, each defining an argument
    :prompt-fn   - Function that implements the prompt logic."
  [mcp-server prompt-map]
  (.removePrompt mcp-server (:name prompt-map))
  (-> (.addPrompt mcp-server (create-async-prompt prompt-map))
      (.subscribe)))

;; helper tool to demonstrate how all this gets hooked together

(defn mcp-server
  "Creates a basic stdio mcp server.

   Optional `instructions` is a non-blank string advertised to the client at
   initialization; nil/blank omits the instructions field."
  ([] (mcp-server nil))
  ([instructions]
   (log/info "Starting MCP server")
   (try
     (let [transport-provider (StdioServerTransportProvider. json-mapper)
           server (cond-> (-> (McpServer/async transport-provider)
                              (.serverInfo "clojure-server" "0.1.11")
                              (.capabilities (-> (McpSchema$ServerCapabilities/builder)
                                                 (.tools true)
                                                 (.prompts true)
                                                 (.resources true true) ;; resources method takes two boolean parameters
                                                 #_(.logging)
                                                 (.build))))
                    (not (string/blank? instructions)) (.instructions instructions)
                    :always (.build))]

       (log/info "MCP server initialized successfully")
       server)
     (catch Exception e
       (log/error e "Failed to initialize MCP server")
       (throw e)))))

(defn load-config-handling-validation-errors
  ([config-file user-dir]
   (load-config-handling-validation-errors config-file user-dir nil))
  ([config-file user-dir config-profile]
   (try
     (config/load-config config-file user-dir config-profile)
     (catch Exception e
       (if (= ::config/schema-error (-> e ex-data :type))
         (let [{:keys [errors file-path]} (ex-data e)]
           (binding [*out* *err*]
             (println "\n❌ Configuration validation failed!\n")
             (when file-path
               (println (str "File: " file-path "\n")))
             (println "Errors found:")
             (doseq [[k v] errors]
               (let [msg (if (sequential? v) (first v) v)]
                 (println (str " 👉 " k " - " msg))))
             (println "\nPlease fix these issues and try again.")
             (println "See CONFIG.md for documentation.\n"))
           (throw e))
         ;; Other error - re-throw
         (throw e))))))

(defn fetch-config [nrepl-client-map config-file cli-env-type env-type project-dir config-profile]
  (let [user-dir (nrepl/fetch-project-directory nrepl-client-map env-type project-dir)]
    (when-not user-dir
      (log/warn "Could not determine working directory")
      (throw (ex-info "No project directory!!" {})))
    (log/info "Working directory set to:" user-dir)

    (let [config (load-config-handling-validation-errors config-file user-dir config-profile)
          final-env-type (or cli-env-type
                             (if (contains? config :nrepl-env-type)
                               (:nrepl-env-type config)
                               env-type))]
      (assoc nrepl-client-map ::config/config (assoc config :nrepl-env-type final-env-type)))))

(def ^:private cli-config-override-keys
  "Keys from the startup opts that override config.edn values."
  [:shadow-cljs-repl-message :enable-tools :disable-tools])

(defn- apply-cli-config-overrides
  "Applies CLI option overrides to the config attached to an nrepl-client-map.
   Only overrides keys that are explicitly provided (non-nil) in opts.

   After absolute overrides, applies relative modifiers:
   - :remove-tools — force-disables tools (adds to :disable-tools, removes from :enable-tools)
   - :add-tools — force-enables tools (removes from :disable-tools, adds to :enable-tools if set)
   :add-tools wins over :remove-tools on overlap."
  [nrepl-client-map opts]
  (let [overrides (reduce (fn [m k]
                            (if (some? (get opts k))
                              (assoc m k (get opts k))
                              m))
                          {}
                          cli-config-override-keys)
        result (if (seq overrides)
                 (update nrepl-client-map ::config/config merge overrides)
                 nrepl-client-map)
        remove-tools (when-let [rt (:remove-tools opts)] (set (map keyword rt)))
        add-tools (when-let [at (:add-tools opts)] (set (map keyword at)))]
    (cond-> result
      ;; remove-tools: force-disable (add to :disable-tools, remove from :enable-tools)
      remove-tools
      (-> (update-in [::config/config :disable-tools]
                     (fn [dt] (vec (distinct (concat (or dt []) remove-tools)))))
          (update-in [::config/config :enable-tools]
                     (fn [et] (when et (vec (remove remove-tools et))))))
      ;; add-tools: force-enable (remove from :disable-tools, add to :enable-tools if set)
      add-tools
      (-> (update-in [::config/config :disable-tools]
                     (fn [dt] (vec (remove add-tools (or dt [])))))
          (update-in [::config/config :enable-tools]
                     (fn [et] (when et (vec (distinct (concat et add-tools))))))))))

(defn create-and-start-nrepl-connection
  "Creates an nREPL client map and loads configuration.

   This function handles:
   - Creating the nREPL client connection (if port provided)
   - Setting up the working directory
   - Loading configuration

   REPL initialization (env detection, init expressions, helpers) happens lazily
   when the first eval-code call is made.

   Takes initial-config map with optional :port, :host, :project-dir, :nrepl-env-type,
   :config-file, :config-profile, :shadow-cljs-repl-message.
   - If :project-dir is provided, uses it directly (no REPL query needed)
   - If :project-dir is NOT provided, queries REPL for project directory (requires :port)
   - If :config-profile is provided, merges profile overlay on top of base config

   Returns the configured nrepl-client-map with ::config/config attached."
  [{:keys [project-dir config-file config-profile port] :as initial-config}]
  (if port
    (log/info "Creating nREPL client for port" port)
    (log/info "Starting without nREPL connection (project-dir mode)"))
  (try
    (let [nrepl-client-map (nrepl/create (apply dissoc initial-config
                                                :project-dir :nrepl-env-type :config-profile
                                                :add-tools :remove-tools
                                                cli-config-override-keys))
          cli-env-type (:nrepl-env-type initial-config)
          _ (log/info "nREPL client map created")
          result (if project-dir
                   ;; Project dir provided - load config directly, no REPL query needed
                   (let [user-dir (.getCanonicalPath (io/file project-dir))
                         _ (log/info "Working directory set to:" user-dir)
                         config (load-config-handling-validation-errors config-file user-dir config-profile)
                         ;; Use cli-env-type or config's env-type, default to :clj
                         final-env-type (or cli-env-type
                                            (:nrepl-env-type config)
                                            :clj)]
                     (assoc nrepl-client-map ::config/config (assoc config :nrepl-env-type final-env-type)))
                   ;; No project dir - need to query REPL (requires port)
                   (let [;; Detect environment type (uses describe op, no full init needed)
                         env-type (nrepl/detect-nrepl-env-type nrepl-client-map)
                         _ (nrepl/set-port-env-type! nrepl-client-map env-type)]
                     (fetch-config nrepl-client-map config-file cli-env-type env-type project-dir config-profile)))]
      (apply-cli-config-overrides result initial-config))
    (catch Exception e
      (log/error e "Failed to create nREPL connection")
      (throw e))))

(defn create-additional-connection
  "Creates a service map for an additional port. Initialization is lazy.
   The returned service shares the base client's state atom and config,
   but targets a different port.

   Note: The third argument (initialize-fn) is deprecated and ignored.
   Initialization now happens lazily via nrepl/ensure-port-initialized!"
  ([nrepl-client-atom initial-config]
   (create-additional-connection nrepl-client-atom initial-config nil))
  ([nrepl-client-atom {:keys [port host]} _deprecated-initialize-fn]
   (log/info "Creating additional connection config for port" port)
   (let [base-client @nrepl-client-atom]
     (assert (::config/config base-client) "Base client must have config")
     (-> base-client
         (assoc :port port)
         (cond-> host (assoc :host host))
         ;; Ensure port entry exists but don't initialize yet (lazy init)
         nrepl/ensure-port-entry!))))

(defn close-servers
  "Convenience higher-level API function to gracefully shut down MCP and nREPL servers.
   
   This function handles the complete shutdown process including:
   - Gracefully closing the MCP server
   - Proper error handling and logging"
  [nrepl-client-atom]
  (log/info "Shutting down servers")
  (try
    (when-let [client @nrepl-client-atom]
      ;; Clean up auto-started nREPL process if present
      (when-let [nrepl-process (:nrepl-process client)]
        (log/info "Cleaning up auto-started nREPL process")
        (nrepl-launcher/destroy-nrepl-process nrepl-process))
      (when-let [mcp-server (:mcp-server client)]
        (log/info "Closing MCP server gracefully")
        (.closeGracefully mcp-server)
        (log/info "Servers shut down successfully")))
    (catch Exception e
      (log/error e "Error during server shutdown")
      (throw e))))

(s/def ::port pos-int?)
(s/def ::host string?)
(s/def ::nrepl-env-type keyword?)
(s/def ::project-dir (s/and string?
                            #(try (let [f (io/file %)]
                                    (and (.exists f) (.isDirectory f)))
                                  (catch Exception _ false))))
(s/def ::config-file (s/and string?
                            #(try (let [f (io/file %)]
                                    (and (.exists f) (.isFile f)))
                                  (catch Exception _ false))))
(s/def ::start-nrepl-cmd (s/coll-of string? :kind vector?))
(s/def ::config-profile (s/or :keyword keyword? :symbol symbol? :string string?))
(s/def ::shadow-cljs-repl-message boolean?)
(s/def ::fallback-nrepl boolean?)
(s/def ::fallback-nrepl-cmd (s/coll-of string? :kind vector?))
(s/def ::fallback-nrepl-dir (s/and string?
                                   #(try (let [f (io/file %)]
                                           (and (.exists f) (.isDirectory f)))
                                         (catch Exception _ false))))
(s/def ::nrepl-args (s/keys :req-un []
                            :opt-un [::port ::host ::config-file ::project-dir ::nrepl-env-type
                                     ::start-nrepl-cmd ::config-profile ::shadow-cljs-repl-message
                                     ::fallback-nrepl ::fallback-nrepl-cmd ::fallback-nrepl-dir]))

(def nrepl-client-atom (atom nil))

(defn coerce-options [{:keys [project-dir config-file] :as opts}]
  (cond-> opts
    (symbol? project-dir)
    (assoc :project-dir (str project-dir))
    (symbol? config-file)
    (assoc :config-file (str config-file))))

(defn validate-options
  "Validates the options map for build-and-start-mcp-server.
   Throws an exception with spec explanation if validation fails."
  [opts]
  (let [opts (coerce-options opts)]
    (if-not (s/valid? ::nrepl-args opts)
      (let [explanation (s/explain-str ::nrepl-args opts)]
        (println "Invalid options:" explanation)
        (log/error "Invalid options:" explanation)
        (throw (ex-info "Invalid options for MCP server"
                        {:explanation explanation
                         :spec-data (s/explain-data ::nrepl-args opts)})))
      opts)))

(defn ensure-port
  "Ensures the args map contains a :port key.
   Throws an exception with helpful context if port is missing.
   
   Args:
   - args: Map that should contain :port
   
   Returns: args unchanged if :port exists
   
   Throws: ExceptionInfo if :port is missing"
  [args]
  (if (:port args)
    args
    (throw
     (ex-info
      "No nREPL port available - either provide :port or configure auto-start"
      {:provided-args args}))))

(defn register-components
  "Registers tools, prompts, and resources with the MCP server, applying config-based filtering.
   
   Args:
   - mcp-server: The MCP server instance to add components to
   - nrepl-client-map: The nREPL client map containing config
   - components: Map with :tools, :prompts, and :resources sequences
   
   Side effects:
   - Adds enabled components to the MCP server
   - Logs debug messages for enabled components
   
   Returns: nil"
  [mcp-server nrepl-client-map {:keys [tools prompts resources]}]
  ;; Register tools with filtering
  (doseq [tool tools]
    (when (config/tool-id-enabled? nrepl-client-map (:id tool))
      (log/debug "Enabling tool:" (:id tool))
      (add-tool mcp-server tool)))

  ;; Register resources with filtering
  (doseq [resource resources]
    (when (config/resource-name-enabled? nrepl-client-map (:name resource))
      (log/debug "Enabling resource:" (:name resource))
      (add-resource mcp-server resource)))

  ;; Register prompts with filtering
  (doseq [prompt prompts]
    (when (config/prompt-name-enabled? nrepl-client-map (:name prompt))
      (log/debug "Enabling prompt:" (:name prompt))
      (add-prompt mcp-server prompt)))
  nil)

(defn build-components
  "Builds tools, prompts, and resources using the provided factory functions.
   
   Args:
   - nrepl-client-atom: Atom containing the nREPL client
   - working-dir: Working directory path
   - component-factories: Map with factory functions
     - :make-tools-fn - (fn [nrepl-client-atom working-dir] ...) returns seq of tools
     - :make-prompts-fn - (fn [nrepl-client-atom working-dir] ...) returns seq of prompts
     - :make-resources-fn - (fn [nrepl-client-atom working-dir] ...) returns seq of resources
   
   Returns: Map with :tools, :prompts, and :resources sequences"
  [nrepl-client-atom working-dir {:keys [make-tools-fn
                                         make-prompts-fn
                                         make-resources-fn]
                                  :as _component-factories}]
  {:tools (when make-tools-fn
            (doall (make-tools-fn nrepl-client-atom working-dir)))
   :prompts (when make-prompts-fn
              (doall (make-prompts-fn nrepl-client-atom working-dir)))
   :resources (when make-resources-fn
                (doall (make-resources-fn nrepl-client-atom working-dir)))})

(defn setup-mcp-server
  "Sets up an MCP server by building components, creating the server, and registering components.
   
   This function encapsulates the common pattern used by both stdio and Streamable HTTP transports:
   1. Build components using factory functions
   2. Create the MCP server (transport-specific)
   3. Register components with filtering
   
   Args:
   - nrepl-client-atom: Atom containing the nREPL client map
   - working-dir: Working directory path
   - component-factories: Map with factory functions (:make-tools-fn, :make-prompts-fn, :make-resources-fn)
   - server-thunk: Zero-argument function that creates and returns a map with :mcp-server
   
   The server-thunk is called AFTER components are built but BEFORE they are registered,
   ensuring components are ready for immediate registration once the server starts.
   
   Returns: The result map from server-thunk (containing at least :mcp-server)"
  [nrepl-client-atom working-dir component-factories server-thunk]
  ;; Build components first to minimize latency
  (let [components (build-components nrepl-client-atom working-dir component-factories)
        ;; Create server after components are ready
        server-result (server-thunk)
        mcp-server (:mcp-server server-result)]
    ;; Register components with filtering
    (register-components mcp-server @nrepl-client-atom components)
    server-result))

(defn build-and-start-mcp-server-impl
  "Internal implementation of MCP server startup.

   Builds and starts an MCP server with the provided configuration.

   This is the main entry point for creating custom MCP servers. It handles:
   - Creating the nREPL client and loading configuration
   - Setting up the working directory
   - Calling factory functions to create tools, prompts, and resources
   - Registering everything with the MCP server

   REPL initialization happens lazily on first eval-code call.

   Args:
   - nrepl-args: Map with connection settings
     - :port (required if no :project-dir) - nREPL server port
     - :host (optional) - nREPL server host (defaults to localhost)
     - :project-dir (optional) - Root directory for the project. If provided, port is optional.

   - component-factories: Map with factory functions
     - :make-tools-fn - (fn [nrepl-client-atom working-dir] ...) returns seq of tools
     - :make-prompts-fn - (fn [nrepl-client-atom working-dir] ...) returns seq of prompts
     - :make-resources-fn - (fn [nrepl-client-atom working-dir] ...) returns seq of resources

   All factory functions are optional. If not provided, that category won't be populated.

   Side effects:
   - Stores the nREPL client in core/nrepl-client-atom
   - Starts the MCP server on stdio

   Returns: nil"
  [nrepl-args component-factories]
  ;; Either :port or :project-dir must be provided (validated by ensure-port-if-needed)
  (let [nrepl-client-map (create-and-start-nrepl-connection nrepl-args)
        working-dir (config/get-nrepl-user-dir nrepl-client-map)
        ;; Store nREPL process (if auto-started) in client map for cleanup
        nrepl-client-with-process (if-let [process (:nrepl-process nrepl-args)]
                                    (assoc nrepl-client-map :nrepl-process process)
                                    nrepl-client-map)
        _ (reset! nrepl-client-atom nrepl-client-with-process)
        ;; Setup MCP server with stdio transport
        server-result (setup-mcp-server nrepl-client-atom
                                        working-dir
                                        component-factories
                                        ;; stdio server creation thunk returns map
                                        (fn [] {:mcp-server (mcp-server
                                                             (config/get-mcp-instructions @nrepl-client-atom))}))
        mcp (:mcp-server server-result)]
    (swap! nrepl-client-atom assoc :mcp-server mcp)
    nil))

(defn ensure-port-if-needed
  "Ensures port is present when project-dir is NOT provided.
   When project-dir IS provided, port is optional (REPL not needed for config loading)."
  [args]
  (if (:project-dir args)
    args ;; project-dir provided, port is optional
    (ensure-port args))) ;; no project-dir, port is required

(defn build-and-start-mcp-server
  "Builds and starts an MCP server with optional automatic nREPL startup.

   This function wraps build-and-start-mcp-server-impl with nREPL auto-start capability.

   If auto-start conditions are met (see nrepl-launcher/should-start-nrepl?), it will:
   1. Start an nREPL server process using :start-nrepl-cmd
   2. Parse the port from process output (if no :port provided)
   3. Pass the discovered port to the main MCP server setup

   Port is only required when :project-dir is NOT provided (need REPL to discover project dir).

   Args:
   - nrepl-args: Map with connection settings and optional nREPL start
     configuration
     - :port (required if no :project-dir) - nREPL server port
     - :host (optional) - nREPL server host (defaults to localhost)
     - :project-dir (optional) - Root directory for the project. If provided, port is optional.
     - :start-nrepl-cmd (optional) - Command to start nREPL server (always
       starts a fresh nREPL; ignored when :fallback-nrepl is also set)
     - :fallback-nrepl (optional) - When true, attempts to attach to
       :port first; if unreachable (or no :port given), spawns a local
       nREPL on an ephemeral port. Does not collide with the configured
       :port. See clojure-mcp.nrepl-launcher/maybe-start-fallback-nrepl.
     - :fallback-nrepl-cmd (optional) - Override the default fallback
       command. Vector of strings. Default uses `clojure` with nREPL
       pulled in via -Sdeps.
     - :fallback-nrepl-dir (optional) - Working directory for the
       spawned fallback REPL. Default: :project-dir if set, else $HOME.

   - component-factories: Map with factory functions
     - :make-tools-fn - (fn [nrepl-client-atom working-dir] ...) returns seq of tools
     - :make-prompts-fn - (fn [nrepl-client-atom working-dir] ...) returns seq of prompts
     - :make-resources-fn - (fn [nrepl-client-atom working-dir] ...) returns seq of resources

   Auto-start conditions (must satisfy ONE):
   1. Both :start-nrepl-cmd AND :project-dir provided in nrepl-args
   2. Current directory contains .clojure-mcp/config.edn with :start-nrepl-cmd
   3. :fallback-nrepl is true (handled separately by the fallback launcher)

   Returns: nil"
  [nrepl-args component-factories]
  (-> nrepl-args
      validate-options
      nrepl-launcher/maybe-start-fallback-nrepl
      nrepl-launcher/maybe-start-nrepl-process
      ensure-port-if-needed
      (build-and-start-mcp-server-impl component-factories)))
